plugins {
    id "java"
}

version = "2.4"
sourceCompatibility = 1.8

repositories {
    jcenter()
}

sourceSets {
    main {
        java { srcDirs "src/" }
        resources { srcDirs "src/" }
    }

    test {
        java { srcDirs "test/" }
        resources { srcDirs "test/" }
    }
}

dependencies {
    implementation "fr.irit.smac.thirdparty.edu.gmu.cs:mason:18"
    implementation "com.formdev:flatlaf:0.36"
    testImplementation "org.mockito:mockito-core:2.+"
    testImplementation "junit:junit:4.12"
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    destinationDir = file("docs/javadoc")
    executable = '/usr/bin/javadoc'
    title = ""
    options.memberLevel = JavadocMemberLevel.PRIVATE
    options.setNoTimestamp(true)
    options.setVersion(true)
    options.setNoDeprecated(true)
    options.setSplitIndex(true)
}

jar {
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes["Main-Class"] = "arcade.gui.ARCADE"
    }
}

task copyJar(type: Copy) {
    from jar
    into project.projectDir
}

build.dependsOn copyJar